#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

// ========== إعداد الشاشة ==========
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
#define I2C_SDA 8
#define I2C_SCL 9

#define BTN_UP     4
#define BTN_DOWN   5
#define BTN_SELECT 6
#define BTN_BACK   7

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// ========== دالة الإعداد ==========
void setup() {
  Wire.begin(I2C_SDA, I2C_SCL);
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  display.clearDisplay();
  display.display();

  pinMode(BTN_UP, INPUT_PULLUP);
  pinMode(BTN_DOWN, INPUT_PULLUP);
  pinMode(BTN_SELECT, INPUT_PULLUP);
  pinMode(BTN_BACK, INPUT_PULLUP);
}

// ========== لوب البرنامج ==========
void loop() {
  handlenfcmenu();
}

// ========== دالة التحكم في القائمة ==========
void handlenfcmenu() {
  // متغيرات القائمة (جوا الدالة)
  const char* menuItems[] = {"Read tag", "Write Data", "Saved Tags", "Emulate Tag", "P2P comunication"};
  const int menuLength = sizeof(menuItems) / sizeof(menuItems[0]);
  const int visibleItems = 3;
  static int selectedItem = 0;
  static int scrollOffset = 0;
  static bool showMessage = false;

  // ===== التعامل مع الأزرار =====
  if (digitalRead(BTN_UP) == LOW) {
    selectedItem--;
    if (selectedItem < 0) selectedItem = menuLength - 1;
    scrollOffset = constrain(selectedItem - visibleItems + 1, 0, menuLength - visibleItems);
    showMessage = false;
  }

  if (digitalRead(BTN_DOWN) == LOW) {
    selectedItem++;
    if (selectedItem >= menuLength) selectedItem = 0;
    scrollOffset = constrain(selectedItem - visibleItems + 1, 0, menuLength - visibleItems);
    showMessage = false;
  }

  if (digitalRead(BTN_SELECT) == LOW) {
    showMessage = true;
  }

  if (digitalRead(BTN_BACK) == LOW) {
    showMessage = false;
  }

  // ===== عرض الشاشة =====
  display.clearDisplay();

  if (showMessage) {
    display.setTextSize(1);
    display.setTextColor(WHITE);
    display.setCursor((SCREEN_WIDTH - 84) / 2, (SCREEN_HEIGHT - 8) / 2);
    display.print("Hi from Hikal");
  } else {
    // عرض عناصر القائمة
    for (int i = 0; i < visibleItems; i++) {
      int menuIndex = i + scrollOffset;
      if (menuIndex >= menuLength) break;

      int y = i * 20;
      if (menuIndex == selectedItem) {
        display.fillRoundRect(6, y, SCREEN_WIDTH - 14, 14, 4, WHITE);
        display.setTextColor(BLACK);
      } else {
        display.setTextColor(WHITE);
      }

      display.setCursor(16, y + 4);
      display.setTextSize(1);
      display.print(menuItems[menuIndex]);
    }

    // رسم شريط التمرير الجانبي بالنقط
    int barX = SCREEN_WIDTH - 4;
    int spacing = SCREEN_HEIGHT / menuLength;

    for (int i = 0; i < menuLength; i++) {
      int dotY = i * spacing + spacing / 2;
      if (i == selectedItem) {
        display.fillRect(barX - 1, dotY - 3, 3, 6, WHITE); // المؤشر
      } else {
        display.drawPixel(barX, dotY, WHITE); // نقطة
      }
    }
  }

  display.display();
  delay(100); // Debounce
}
